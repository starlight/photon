(module
  photon.stream
  (stream%
    %stream
    %stream?
    %lseq
    %pos
    port->stream%
    result%
    %result
    %result?
    %value
    %input
    *return
    *fail
    *identity
    *bind
    *one-of
    *null
    *maybe
    *each-of
    *zero-or-more
    *one-or-more
    *is)

  (import
    r5rs
    utf8
    srfi-2
    srfi-9
    srfi-127
    srfi-158)

  ; ğ’‘ -> ğ’
  (define (port->lseq port)
    (generator->lseq
      (make-coroutine-generator
        (lambda (yield)
          (let loop
            ((ch (read-char port)))
            (cond
              ((eof-object? ch))
              (else
                (yield ch)
                (loop (read-char port)))))))))

  ; ğ’ ğ‘› -> ğœ¸â‚™
  (define-record-type stream%
    (%stream lseq pos)
    %stream?
    (lseq %lseq)
    (pos %pos))

  ; ğ’‘ -> ğœ¸â‚€
  (define (port->stream% port)
    (%stream (port->lseq port) 0))

  ; ğ‘£ ğœ¸â‚™ -> ğ‘£.ğœ¸â‚™
  (define-record-type result%
    (%result value input)
    %result?
    (value %value)
    (input %input))

  ; ğ‘£ -> ğœ¸â‚™ -> ğ‘£.ğœ¸â‚™
  (define ((*return value) input)
    (%result value input))

  ; -> ğœ¸â‚™ -> âŠ¥
  (define ((*fail) input) #f)

  ; -> ğœ¸â‚™ -> ğ‘¥â‚™.ğœ¸â‚™â‚Š | âŠ¥
  (define ((*identity) input)
    (cond
      ((null? (%lseq input)) #f)
      (else
        (%result
          (lseq-car (%lseq input))
          (%stream
            (lseq-cdr (%lseq input))
            (+ 1 (%pos input)))))))

  ; >>=
  ; ğ‘ ğ‘˜' => ğ‘£â‚š -> ğ‘£â‚– | âŠ¥
  (define ((*bind parser combinator) input)
    (and-let*
      ((result (parser input)))
      ((combinator (%value result)) (%input result))))

  ; <|>
  ; ğ‘ ğ‘Ÿ* => ğ‘£â‚š | ğ‘£áµ£... | âŠ¥
  (define ((*one-of parser . parsers) input)
    (let
      ((result (parser input)))
      (cond
        ((or result (null? parsers)) result)
        (else
          ((apply *one-of parsers) input)))))

  ; => âˆ…
  (define (*null)
    (*return '()))

  (define (*each-of . parsers)
    (cond
      ((null? parsers) (*null))
      (else
        (*bind
          (car parsers)
          (lambda (value)
            (*bind
              (apply *each-of (cdr parsers))
              (lambda (value')
                (*return (cons value value')))))))))

  ; ğ‘ => ğ‘£â‚š | âˆ…
  (define (*maybe parser)
    (*one-of parser (*null)))

  (define (*one-or-more parser)
    (*each-of parser (*zero-or-more parser)))

  (define (*zero-or-more parser)
    (*maybe
      (*one-or-more parser)))

  (define (*is predicate . args)
    (*bind
      (*identity)
      (lambda (value)
        (if (apply predicate value args)
          (*return value)
          (*fail)))))

)
