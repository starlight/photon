(module
  photon.stream
  (stream%
    %stream
    %stream?
    %lseq
    %pos
    generator->stream%
    result%
    %result
    %result?
    %value
    %rest
    *return
    *fail
    *identity
    *bind
    *one-of
    *nothing
    *maybe
    *is
    *not
    *end
    *list
    *zero-or-more
    *one-or-more)

  (import
    r5rs
    utf8
    srfi-2
    srfi-9
    srfi-127
    srfi-158)

  (define-record-type stream%
    (%stream lseq pos)
    %stream?
    (lseq %lseq)
    (pos %pos))

  (define-record-type result%
    (%result value stream)
    %result?
    (value %value)
    (stream %rest))

  (define (generator->stream% gen)
    (%stream (generator->lseq gen) 0))

  (define (%empty? stream)
    (null? (%lseq stream)))

  (define (port->generator port)
    (make-coroutine-generator
      (lambda (yield)
        (let loop
          ((char (read-char port)))
          (cond
            ((eof-object? char))
            (else
              (yield char)
              (loop (read-char port))))))))

  ; ğ‘£ -> ğœ¸â‚“ -> ğ‘£.ğœ¸â‚“
  ; ğ‘£ => ğ‘£
  (define ((*return value) input)
    (%result value input))

  ;  -> ğœ¸â‚“ -> âŠ¥
  ;  => âŠ¥
  (define ((*fail) input)
    #f)

  ;  -> ğœ¸â‚“ -> ğ‘¥.ğœ¸â‚Š | âŠ¥
  ;  => ğ‘¥ | âŠ¥
  (define ((*identity) input)
    (if (%empty? input) #f
      (%result
        (lseq-car (%lseq input))
        (%stream
          (lseq-cdr (%lseq input))
          (+ 1 (%pos input))))))

  ; ğ‘Ÿ ğ‘˜' => ğ‘˜'ğ‘£Ê³ -> ğ‘£áµ | âŠ¥
  (define ((*bind parser combinator) input)
    (and-let*
      ((result (parser input)))
      ((combinator (%value result)) (%rest result))))

  ; ğ‘Ÿ ğ‘ .. => ğ‘£Ê³ | ğ‘£Ë¢.. | âŠ¥
  (define ((*one-of . parsers) input)
    (and
      (not (null? parsers))
      (or
        ((car parsers) input)
        ((apply *one-of (cdr parsers)) input))))

  ;  => âˆ…
  (define (*nothing)
    (*return '()))

  ; ğ‘Ÿ => ğ‘£Ê³ | âˆ…
  (define (*maybe parser)
    (*one-of parser (*nothing)))

  ; ğ‘Ÿ => ğ‘£Ê³ âˆ´ âŠ¥ | âˆ…
  (define ((*not parser) input)
    (and
      (not (parser input))
      ((*nothing) input)))

  ;  => ğ‘¥ âˆ´ âŠ¥ | âˆ…
  (define (*end)
    (*not (*identity)))

  ; ğ‘’ => ğ‘’ğ‘¥ âˆ´ ğ‘¥ | âŠ¥
  (define (*is predicate . args)
    (*bind
      (*identity)
      (lambda (value)
        (if (apply predicate value args)
          (*return value)
          (*fail)))))

  ; ğ‘Ÿ ğ‘ .. => [ğ‘£Ê³, ğ‘£Ë¢..] | âŠ¥
  (define (*list . parsers)
    (if (null? parsers) (*nothing)
      (*bind
        (car parsers)
        (lambda (value)
          (*bind
            (apply *list (cdr parsers))
            (lambda (value')
              (*return (cons value value'))))))))

  ; ğ‘Ÿ => [ğ‘£Ê³, <ğ‘£Ê³..>] | âŠ¥
  (define (*one-or-more parser)
    (*bind
      parser
      (lambda (value)
        (*list
          (*return value)
          (*zero-or-more parser)))))

  ; ğ‘Ÿ => [ğ‘£Ê³, <ğ‘£Ê³..>] | âˆ…
  (define (*zero-or-more parser)
    (*maybe
      (*one-or-more parser)))

)
